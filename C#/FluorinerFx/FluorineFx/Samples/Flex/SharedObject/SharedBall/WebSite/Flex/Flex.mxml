<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="connect()" width="100%" height="100%" >
	<mx:Script>
		<![CDATA[
			private var readyForDrag:Boolean = false;
			private var so:SharedObject;
			private var nc:NetConnection;
			
			NetConnection.defaultObjectEncoding = flash.net.ObjectEncoding.AMF0;
			SharedObject.defaultObjectEncoding  = flash.net.ObjectEncoding.AMF0;
			
			//NOTE
			//ObjectEncoding.AMF3 works correctly with Flash Player 9,0,60,120 installed
			//at least could not make it work with 9,0,47,0
			
			private function connect():void
			{
				nc = new NetConnection();
				nc.connect("rtmp://localhost:1937/SharedBall");
			    so = SharedObject.getRemote("BallControl", nc.uri, false);
			    so.client = this;
			    so.addEventListener( SyncEvent.SYNC, handleSync );
			    so.connect( nc );
			}
			 
			private function handleSync( event:SyncEvent ):void
			{
				if( event.changeList.length == 0 )
					return;
				if( event.changeList[0].code == "clear" )
					return;
				image.x = so.data.ballCoordinates.x;
				image.y = so.data.ballCoordinates.y;  
			}
			
			private function handleMouseDown( event:MouseEvent ):void
			{
				readyForDrag = true;				
				image.startDrag( false, null );
			}
			
			private function handleMouseMove( event:MouseEvent ):void
			{
				if( !readyForDrag )
					return;
				
				var obj:Object = new Object();
				obj.x = image.x;
				obj.y = image.y;
				so.setProperty( "ballCoordinates", obj );
			}
			
			private function handleMouseUp( event:MouseEvent ):void
			{
				readyForDrag = false;
				image.stopDrag();
			}			
			
		]]>
	</mx:Script>
	<mx:Canvas width="100%" height="100%" mouseMove="handleMouseMove(event)"
				mouseUp="handleMouseUp(event)" id="container" backgroundColor="#ffffff" horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<mx:Image x="80" y="65" source="ball.png" id="image" 
			mouseDown="handleMouseDown(event)" />
		
	</mx:Canvas>
</mx:Application>
